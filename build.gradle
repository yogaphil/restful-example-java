import com.github.spotbugs.SpotBugsTask

buildscript {
	repositories {
		mavenCentral()
	}
}

plugins {
    id "checkstyle"
    id "java"
    id "pmd"
    id "com.github.spotbugs" version "1.6.5"
    id "org.springframework.boot" version "2.1.0.RELEASE"
    id "org.jetbrains.gradle.plugin.idea-ext" version "0.4.2"
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
    id "org.owasp.dependencycheck" version "4.0.0"
}

group = 'com.philwest.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

dependencies {
    // avoid security vulnerability in older guava referenced by checkstyle by forcing newer dep
    compile 'com.google.guava:guava:27.0-jre'

	implementation 'org.springframework.boot:spring-boot-starter-jersey'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	runtimeOnly 'org.springframework.boot:spring-boot-devtools'
	compileOnly 'org.projectlombok:lombok'
	compile 'io.springfox:springfox-swagger2:2.9.2'
    compile 'io.springfox:springfox-swagger-ui:2.9.2'
    compile 'com.google.code.findbugs:jsr305:1.3.9'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

pmd {
    pmdTest.enabled=false
}

spotbugs{
    toolVersion = '3.1.8'
    effort = "max"
    reportLevel = "high"
}

tasks.withType(SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

checkstyle {
    toolVersion '8.14'
    configFile file("config/checkstyle/checkstyle.xml")
}

checkstyleMain {
    source ='src/main/java'
}

checkstyleTest {
    source ='src/test/java'
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}
